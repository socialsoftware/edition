package pt.ist.socialsoftware.edition.domain;

enum pt.ist.socialsoftware.edition.domain.Reading.ReadingType as ReadingType;
enum pt.ist.socialsoftware.edition.domain.ManuscriptSource.Form as Form;
enum pt.ist.socialsoftware.edition.domain.ManuscriptSource.Material as Material;
enum pt.ist.socialsoftware.edition.domain.SpaceText.SpaceDim as SpaceDim;
enum pt.ist.socialsoftware.edition.domain.SpaceText.SpaceUnit as SpaceUnit;
enum pt.ist.socialsoftware.edition.domain.FormatText.Rendition as Rendition;
enum pt.ist.socialsoftware.edition.domain.LdoDText.OpenClose as OpenClose;
enum pt.ist.socialsoftware.edition.domain.AddText.Place as Place;
enum pt.ist.socialsoftware.edition.domain.DelText.HowDel as HowDel;

class LdoD {
	String title;
	String author;
	String editor;
	String sponsor;
	String principal;
	String funder;
}

class Edition {
	String xmlId;
	String title;
	String author;
	String editor;
	String date;
}

relation LdoDHasEditions {
	LdoD playsRole ldoD {multiplicity 1..1;}
	Edition playsRole editions {multiplicity 0..*;}
}

class Taxonomy {
	String xmlId;
	String name;
}

class Category {
	String xmlId;
	String name;
}

relation LdoDHasTaxonomies {
	LdoD playsRole ldoD {multiplicity 1..1;}
	Taxonomy playsRole taxonomies {multiplicity 0..*;}
}

relation TaxonomyHasCategories {
	Taxonomy playsRole taxonomy {multiplicity 1..1;}
	Category playsRole categories {multiplicity 0..*;}
}

class Heteronym {
	String xmlId;
	String name;
}

relation LdoDHasHeteronyms {
	LdoD playsRole ldoD {multiplicity 1..1;}
	Heteronym playsRole heteronyms {multiplicity 0..*;}
}

class Fragment {
	String xmlId;
	String title;
}

relation LdoDHasFragments {
	LdoD playsRole ldoD {multiplicity 1..1;}
	Fragment playsRole fragments {multiplicity 0..*;}
}

class FragInter {
	String xmlId;
	String date;
}

relation FragmentHasFragInters {
	Fragment playsRole fragment {multiplicity 1..1;}
	FragInter playsRole fragmentInter {multiplicity 0..*;}
}

relation FragInterAttributesHeronym {
	FragInter playsRole fragInter {multiplicity 0..*;}
	Heteronym playsRole heteronym {multiplicity 0..1;}
}

relation FragInterHasCategories {
	FragInter playsRole fragInters {multiplicity 0..*;}
	Category playsRole categories {multiplicity 0..*;}
}

class EditionInter extends FragInter {
	String title;
	int number;
	int page;
	String notes;
}

class SourceInter extends FragInter;

relation EditionHasEditionInters {
	Edition playsRole edition {multiplicity 1..1;}
	EditionInter playsRole editionInters {multiplicity 0..*;}
}

class Source {
	String xmlId;
	Boolean hasLdoDLabel;
}

relation FragmentHasSources {
	Fragment playsRole fragment {multiplicity 1..1;}
	Source playsRole sources {multiplicity 0..*;}
}

relation SourceHasSourceInters {
	Source playsRole source {multiplicity 1..1;}
	SourceInter playsRole sourceInters {multiplicity 0..*;}
}

class ManuscriptSource extends Source {
	String settlement;
	String repository;
	String idno;
	String altIdentifier;
	Form form;
	Material material;
	int columns;
	String notes;
}

class PrintedSource extends Source {
	String title;
	String pubPlace;
	String issue;
	String date;
}

class VariationPoint {
}

class Reading {
	ReadingType type;
}

relation FragmentHasVariationPoint {
	Fragment playsRole fragment {multiplicity 0..1;}
	VariationPoint playsRole variationPoint {multiplicity 0..1;}
}

relation VariationPointHasInReadings {
	VariationPoint playsRole nextVariationPoint {multiplicity 1..1;}
	Reading playsRole inReadings {multiplicity 0..*;}
}

relation VariationPointHasOutReadings {
	VariationPoint playsRole previousVariationPoint {multiplicity 1..1;}
	Reading playsRole outReadings {multiplicity 0..*;}
}

relation FragInterHasReadings {
	FragInter playsRole fragInters {multiplicity 0..*;}
	Reading playsRole readings {multiplicity 0..*;}
}

class LdoDText {
	OpenClose openClose;
}

relation ReadingHasText {
	Reading playsRole readingOfFirst {multiplicity 0..1;}
	LdoDText playsRole firstText {multiplicity 1..1;}
}

relation TextBelongs2Reading {
	Reading playsRole reading {multiplicity 1..1;}
	LdoDText playsRole text {multiplicity 0..*;}
}

relation TextHasText {
	LdoDText playsRole prevText {multiplicity 0..1;}
	LdoDText playsRole nextText {multiplicity 0..1;}
}

class SimpleText extends LdoDText {
	String value;
}

class LbText extends LdoDText {
	Boolean breakWord;
	Boolean hyphenated;
}

class PbText extends LdoDText;

class SpaceText extends LdoDText {
	SpaceDim dim;
	int quantity;
	SpaceUnit unit;
}

class FormatText extends LdoDText {
	Rendition rend;
}

class ParagraphText extends LdoDText {
}

class EmptyText extends LdoDText {
	Boolean isBreak;
}

class AddText extends LdoDText {
	Place place;
}

class DelText extends LdoDText {
	HowDel how;
}

class SubstText extends LdoDText {
}